	INCLUDE	START

	PUSHORG	ROMSAVE

	SUBTTL	TUNE ROUTINE

* new tune		pass tune address in word after call
	ROUTINE	NEWTUNE
	PSHS	D,X,U		> uses none
	LDX	6,S		get tune address and advance return stack
	LDU	X++
	STX	6,S
	LDA	AMDEMO		in attract mode demo?
	BEQ	1$
	CMPU	#QCOIN		yes, is it coin sound?
	BNE	2$		only play coin sound in attract mode
1$	LDA	U+		check priority
	CMPA	TNPRIO
	BLS	2$
	CLRA			clear repeat counters addresses
.$$	SET	TNREPT
	REPT	NTNREPT
	STA	.$$
.$$	SET	.$$+OTNRLEN
	ENDR
	BSR	TNLOAD		load tune, save none, use A,B,X,U
2$	PULS	D,X,U,PC

* tune loader		pass tune address in U
TNLOAD	LDA	U		fetch first instruction > uses A,B,X,U
	BITA	#0C0		repeat or done?
	BNE	7$
	TSTA			yes, done?
	BNE	1$
	CLR	TNPRIO		zero priority
	LDA	#-1		clear sound lines
	STA	PIA+0E
	LDA	#1E!X1F		background sound off, for now
	STA	PIA+0E
	RTS			quit
1$	LDX	#TNREPT		repeat, find counter
2$	CMPU	X		is this the counter?		
	BEQ	5$
	LEAX	OTNRLEN,X	no, check next
	CMPX	#TNREPT+NTNREPT*OTNRLEN
	BNE	2$
	LDX	#TNREPT		new repeat counter, find available counter
3$	LDB	X		this one available?
	BEQ	4$
	LEAX	OTNRLEN,X	no, check next
	BRA	3$
4$	STU	X		make this one counter
	STA	OTNRCNT,X	
5$	DEC	OTNRCNT,X	decrement this repeat counter
	BEQ	6$		done?
	LDA	1,U		repeat, find offset
	LEAU	A,U		add to tune program counter
	BRA	TNLOAD		load next instruction
6$	CLR	X		yes, clear repeat counter address to free it
	LEAU	2,U		advance tune program counter
	BRA	TNLOAD		load next instruction 
7$	BPL	8$		sound?
	LDB	#-1		yes?, clear sound lines
	STB	PIA+0E
* THIS IS WHERE THE SOUND IS ACTUALLY PLAYED
	STA	PIA+0E		play sound
	LEAU	1,U		advance tune program counter
	BITA	#40		time?
	BEQ	TNLOAD
	BRA	9$		yes, set timer
8$	LDA	U+		set priority
	ANDA	#3F
	STA	TNPRIO
9$	LDA	U+		set tune timer
	STA	TNTIME
	STU	TNPC		save tune program counter
	RTS

	PULLORG	ROMSAVE
