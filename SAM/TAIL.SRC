	INCLUDE	START
	INCLVER	SAM,SINISTAR.EQU
	INCLVER	SAM,PLSUNIT

	PUSHORG	SRAMSAV
TAILDAT	RMB	140		tail random data
	PULLORG	SRAMSAV

	PUSHORG	BASSAV
TAILL	RMB	1	\	tail l screen position
TAILS	RMB	1	/	tail s screen position
TAILDMA	RMB	1		tail flavor
PLDRWVE	RMB	2		player erase-update-draw vector
	PULLORG	BASSAV

	PUSHORG	RAMSAV
TAILBUF	RMB	3*5		tail data buffer
	PULLORG	RAMSAV

	PUSHORG	ROMSAVE

* erase tail on player ship
ETAIL	LDX	OIDESC,Y
	LDD	#305!X404	* * * FOR DMA BUG
	STD	DHIGH
	LDD	TAILL
	STD	DDEST
	LDD	#TAILBUF
	STD	DSOUR
	LDA	TAILDMA
	ORA	#DCNS
	STA	DCTRL
	JMP	[PLDRWVE]

* change image of tail on player ship
CTAIL	LDD	#ETAIL
	STD	ODRWVEC,Y
	LDD	#DOBJECT
	STD	PLDRWVE
	LDD	#TAIL
	STD	OPDRWVE,Y
* draw tail in player ship
TAIL	LDB	PLACCEL
	LBEQ	NOPOST
	ASLB
	CLRA
	ADDD	#TAILDAT
	TFR	D,U
	FRANDOM	OSEED1,BYTE
	ANDA	#3F
	LEAU	A,U
	STU	DSOUR
	LDD	#TAILBUF
	STD	DDEST
	LDD	#305!X404	* * * FOR DMA BUG
	STD	DHIGH
	LDA	PMEMCTL
	PSHS	A
	ANDA	#!N4
	STA	PMEMCTL
	STA	MEMCTL
	CLR	DCTRL
	LDD	#IDSBOMB
	STD	DSOUR
	LDA	#DXWT+DXWB+DCNS+DZSP
	STA	DCTRL
*<<< optimize this >>>
	PULS	A
	STA	PMEMCTL
	STA	MEMCTL
	LDD	#TAILBUF
	STD	DSOUR
	LDB	OIDESC+1,Y
	SUBB	#IPLAYER!.$FF
	LSRB
	LSRB
	LDX	#TAILTBL
	ABX
	LDA	1,X
	ADDA	OLEFT,Y
	STA	DDEST+1
	STA	TAILS
	LDA	X
	CLRB
	ASRA
	RORB
	ADDD	OLPOS,Y
	STA	DDEST
	STA	TAILL
	LDA	#DZSP+DSCY+DWBL	
	BCS	1$
	ORA	#DTFL
1$	STA	DCTRL	
	STA	TAILDMA
	JMP	NOPOST

	LOCAL
* initialize random tail data
	VECTOR	SYSVECT
	LDX	#TAILDAT
	CLRA
	LDB	#0C0
	JSR	INITRAM
	JSR	INITRAM
	LDX	#PALETTE
	CLR	TEMP
1$	LDA	X
	BEQ	2$
	BITA	#0C0
	BEQ	6$
	BITA	#30
	BNE	2$
6$	LDA	#40
	STA	TEMP+1
5$	RANDOM	OSEED1,BYTE
	ANDA	#3F
	JSR	SINCOS
	CLRB
	JSR	ASRD6
	PSHS	D
	RANDOM	OSEED2,BYTE
	TFR	A,B
	ANDB	#7F
	CLRA
	ADDD	S++
	LSRA
	RORB
	LDU	#TAILDAT
	LEAU	D,U
	LDA	U
	BCS	3$
	ANDA	#0F
	STA	TEMP+2
	LDA	TEMP
	RPT	4,ASLA
	ORA	TEMP+2	
	BRA	4$
3$	ANDA	#0F0
	ORA	TEMP
4$	STA	U
	DEC	TEMP+1
	BNE	5$
2$	INC	TEMP
	LEAX	1,X
	CMPX	#PALETTE+10
	BLO	1$
	RTS

	VECTOR	INIVECT
	CLRD
	STD	TAILL		zero tail screen position
*<<< add to player initialization >>>
	LDD	#DOBJECT	initialize player erase-update-draw vector
	STD	PLDRWVE
	LDD	#ETAIL		give player erase tail erase-update-draw vector
	STD	ODRWVEC+WPLAYER
	LDD	#TAIL		give player tail post-draw vector
	STD	OPDRWVE+WPLAYER
	RTS

* patch to player image rotation
	ERR	OLD ADDRESS
	PUSHORG	$3789
	JSR	100$
	PULLORG
100$
* * *
	STX	PLDRWVE
	LDD	#CTAIL		give player change tail post-draw vector
	STD	OPDRWVE,Y
* * *
	RTS

* tail offset table
TAILTBL	FDB	$FC02,$FC02,$FD01,$FDFF,$FFFE,$FFFD,$00FD,$FFFD
	FDB	$02FB,$02FD,$05FD,$05FD,$07FE,$08FF,$0900,$0A01
	FDB	$0A03,$0A02,$0905,$0905,$0707,$0509,$040A,$020A
	FDB	$020A,$020A,$010A,$0009,$FF08,$FD05,$FD05,$FC02

*<<< for Noah >>>
	ERR	OLD ADDRESS
	PUSHORG	$6F51
	JSR	101$
	PULLORG
101$
* * *
	LDD	#NOPOST		turn off player's tail
	STD	OPDRWVE,X
* * *
  	JSR	PLXQUE			* START UP PLAYER EXPLOSION
	RTS

	PULLORG	ROMSAVE
